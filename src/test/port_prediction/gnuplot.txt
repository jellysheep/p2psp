
set term pdf enhanced font "Times,25" size 20,12
#~ set term pngcairo enhanced font "Times,15" size 1800,1000

set key right top
set grid xtics ytics
set style line 1 lc rgb "black" lw 1
set style line 2 lc rgb "red" lw 1
set style line 3 lc rgb "blue" lw 1 pointtype 8
set style line 4 lc rgb "green" lw 1
set style line 5 lc rgb "orange" lw 1
set style line 6 lc rgb "purple" lw 1
set style fill solid

set key right bottom height 0.5

set ylabel "Port prediction success rate"
set yrange [0:1];

f1(x) = 1-a1*exp(b1*x)
fit f1(x) 'results.txt' using 1:2 via a1,b1
f2(x) = 1-a2*exp(b2*x)
fit f2(x) 'results.txt' using 1:3 via a2,b2

FIT_LIMIT=1.e-16
d3=-0.7
f3(x) = (1-exp(a3*(x**b3)))
fit f3(x) 'results3.txt' using 1:2 via a3,b3
d4=-0.7
f4(x) = (1-exp(a4*(x**b4)))
fit f4(x) 'results3.txt' using 1:3 via a4,b4

set bmargin 4
set rmargin 8
set out "plot.pdf"
set multiplot layout 2,3

set title "2 peers, avg. 0.5 port skips between monitors, \navg. 4 port skips between peers, port step 1"
set xlabel "Number of guessed source ports"
set xrange [1:10];
plot \
    'results.txt' using 1:3 ls 2 title "2 monitors", \
    'results.txt' using 1:2 ls 3 title "1 monitor", \
    f2(x) ls 2 title "Fit 1-a*e^{bx} (2 mon.)", \
    f1(x) ls 3 title "Fit 1-a*e^{bx} (1 mon.)"
    #f2(x)/f1(x)-1 ls 6 title "Relative success rate improvement"

set title "2 peers, avg. 4 port skips between peers, \n10 guessed source ports, port step 1"
set xlabel "Average port skips between monitors"
set xrange [0:20];
plot \
    'results2.txt' using 1:3 ls 2 title "2 monitors", \
    'results2.txt' using 1:2 ls 3 title "1 monitor"

set title "2 peers, avg. 0.5 port skips between monitors, \n10 guessed source ports, port step 1"
set key left bottom
set xlabel "Average port skips between peers"
set xrange [0:20];
plot \
    'results3.txt' using 1:3 ls 2 title "2 monitors", \
    'results3.txt' using 1:2 ls 3 title "1 monitor", \
    f4(x) ls 2 title "Fit 1-e^{a*x^b} (2 mon.)", \
    f3(x) ls 3 title "Fit 1-e^{a*x^b} (1 mon.)"

set title "2 peers, avg. 0.5 port skips between monitors, \navg. 4 port skips between peers, 10 guessed source ports"
set key right bottom
set xlabel "Number of monitors"
set xrange [1:10];
plot \
    'results4.txt' using 1:2 ls 3 title "port step 1", \
    'results4.txt' using 1:3 ls 2 title "port step 2"

set title "2 peers, avg. 0.5 port skips between monitors, \navg. 4 port skips between peers, 10 guessed source ports"
set key right top
set xlabel "Port step"
set xrange [1:10];
plot \
    'results5.txt' using 1:2 ls 3 title "1 monitor", \
    'results5.txt' using 1:3 ls 2 title "2 monitors"
